INFO {
  TITLE:       "Parameterized Collector (binary encoded version)"
  DESCRIPTION: "Signals whether all input clients have delivered a token"
  SEMANTICS:   Mealy
  TARGET:      Mealy
}

GLOBAL {
  PARAMETERS {
    n = 4;
  }

  DEFINITIONS {
    /* Checks whether a bus of size log(n) currently represents
     * the numerical value v (encoded in binary).
     */
    value(bus,v) = value'(bus,v,0,SIZEOF bus);
    value'(bus,v,i,j) =
      i >= j        : true
      bit(v,i) == 1 : value'(bus,v,i + 1,j) &&  bus[i]
      otherwise    : value'(bus,v,i + 1,j) && !bus[i];

    /* Returns the i-th bit of the numerical value v.
     */
    bit(v,i) =
      i <= 0     : v % 2
      otherwise : bit(v / 2,i - 1);

    /* Discrete logarithm.
     */
    log2(x) =
      x <= 1     : 1
      otherwise : 1 + log2(x / 2);
  }  
}

MAIN {

  INPUTS {
    finished[log2(n-1)];
  } 

  OUTPUTS {
    allFinished; 
  }

  PRESET {
    &&[0 <= i < n]
      (!allFinished W value(finished, i));
  }

  ASSERT {
    &&[0 <= i < n] G F value(finished, i)
      -> G F allFinished;
  
    &&[0 <= i < n] 
      (allFinished -> X (!allFinished W value(finished, i)));
  }
}

//#!SYNTCOMP
//STATUS : realizable
//REF_SIZE : -1
//#.
